*** Settings ***
Resource    ${CURDIR}/../resources/config/env_setup.txt
Resource    ${CURDIR}/../resources/fixtures/fixture_and_faker.txt
Resource    ${CURDIR}/../resources/selenium_wrapper.txt
Resource    ${CURDIR}/../resources/restapi_keywords.txt

*** Test Cases ***
Global Values in Managed Test Plan
    [Tags]      fast     global_value
    Go To Edit Test Plan Page
    Open Directory  Utilities
    Open Directory  Samples
    Add Snippet     sleeptest
    Verify Snippet sleeptest Added In Test Layout Order 1
    Click Order 1 Snippet To Edit Ckeys
    Fillin Ckey seconds With Value 5
    Set Ckey seconds As Global
    Verify Ckey seconds Set As Global
    Fillin Ckey timeout With Value 30s
    Verify Ckey timeout Set As Local
    Verify Ckey sleep_on Set As Local
    Click Save In Ckey Window
    Verify Snippet In Order 1 Status Should Be "Ready to Submit"
    Click Add SUT In SUT Section
    Pick SUT    ${hw[0]['name']}
    Verify SUT Show Up In SUT Section Row 1 With Correct Data ${hw[0]}
    ${testplan name}=   Get Random Word
    Save Test Plan As ${testplan name}
    Go To Manage Test Plans Page
    Verify Test Plan ${testplan name} Ckey seconds is Global
    ${result}=    Get Data From JSON  ${CURDIR}/../testdata/global_values_in_manage_test_plan.json
    Set To Dictionary   ${result[0]}    name=${testplan name}
    Verify Test Plan ${testplan name} Has Correct Data ${result[0]}
    Delete Test Plan ${testplan name}
    Verify Test Plan ${testplan name} Not Exist
