*** Settings ***
Library     String
Library     OperatingSystem
Library     FakerLibrary
Library     Collections

*** Keywords ***
Get Data From JSON
    [Arguments]    ${fn}
    [Documentation]     {External}
    ...                 Read JSON file ${fn} locally.
    ...                 Example:
    ...                 | ${data}= | Get Data From JSON | /home/my_directory/myjson.json |
    ${tp}=      OperatingSystem.Get File    ${fn}
    ${ret}=     Evaluate    json.loads(r'''${tp}''')    json
    [Return]    ${ret}

Get Data From JSON And Push Random Name Values
    [Arguments]    ${fn}
    [Documentation]     {External}
    ...                 Read JSON file ${fn} locally and will replace the value of dictionary key "name" to a random word.
    ...                 Example:
    ...                 ${data}= {"name":"test", "type":generic, "desc":"test string"}
    ...                 | ${data}= | Get Data From JSON And Push Random Name Values | /home/my_directory/myjson.json |
    ...                 ${data}= {"name":"civil68", "type":generic, "desc":"test string"}
    ${ret}=     Get Data From JSON          ${fn}
    Replace Name Value With Random Word     ${ret}
    [Return]    ${ret}

Replace Name Value With Random Word
    [Arguments]     ${data}
    [Documentation]     {Internal}
    ...             Read mutiple dictionaries ${data} in and add/update the value of the key "name" with random word genrated by FakerLibrary.User Name.
    :FOR    ${items}    IN  @{data}
    \   ${new_name}=    FakerLibrary.User Name
    \   Set To Dictionary   ${items}    name=${new_name}
    \   Log     Add/Update name with value \'"${new_name}"\'

Get Random Word
    ${word}=    FakerLibrary.Words    nb=${1}
    Log     Generate a random word: ${word}\n
    [Return]    ${word[0]}

Get Data From JSON And Assign Testplan Name
    [Arguments]    ${fn}    ${owner}=administrator
    ${ret}=     Get Data From JSON          ${fn}
    ${word}=    Get Random Word
    ${random_dict}=    Create Dictionary   sut_required=true   name=${word}    login_name=${owner}
    Set To Dictionary   ${ret}   info=${random_dict}
    [Return]    ${ret}
